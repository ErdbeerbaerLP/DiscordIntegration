plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "com.modrinth.minotaur" version "2.+" apply false
}
architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version


}
subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'
    apply plugin: 'com.modrinth.minotaur'
    apply plugin: 'com.github.johnrengelman.shadow'
    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }
    repositories {
        maven { url "https://repo.erdbeerbaerlp.de/repository/maven-public/" }

    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }
    modrinth{
        if (project.hasProperty('modrinth.apikey')) { // $GRADLE_USER_HOME/gradle.properties
            token = project.getProperty("modrinth.apikey") // Use an environment property!
            projectId = 'rbJ7eS5V'
            versionNumber = "${project.mod_version}-${project.minecraft_version}"
            versionName = "DiscordIntegration ${project.mod_version} (MC ${project.minecraft_version})"
            uploadFile = remapJar
            changelog = project.changelog
            gameVersions = ["${project.minecraft_version}"]
            loaders = ['fabric', 'forge']
            dependencies {
                required.project "fabric-api"
                optional.project "no-chat-reports"
            }

            versionType = project.releaseType.toUpperCase()
        }
    }
}
